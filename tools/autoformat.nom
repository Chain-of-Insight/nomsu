#!/usr/bin/env nomsu -V3.6.5.6
#
    Auto-format Nomsu code. Usage:
        nomsu tools/autoformat.nom [-i] file1 file2 directory1 ...
    If the first argument is "-i", modifications will be performed in-place. Otherwise,
    the formatted code will be printed.

use "lib/os.nom"

%args = (command line args)
%inplace = (no)
if (%args.1 is "-i"):
    %inplace = (yes)
    %args::remove index 1

for %path in %args:
    for file %filename in %path:
        unless (%filename matches "%.nom$"): do next %filename
        %formatted = ".."
            #!/usr/bin/env nomsu -V\(Nomsu version)
            \((parse (read file %filename) from %filename) as nomsu)
        
        if %inplace:
            write %formatted to file %filename
        ..else: say %formatted
