#!/usr/bin/env nomsu -V4.10.12.7
#
    Tool to run all tests in a file (i.e. the code block inside a call to 'test %'). Usage:
    nomsu tools/test.nom file1 file2 directory1 ...
    
use "lib/os.nom"
use "lib/consolecolor.nom"

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

%args = (command line args)
if (%args.1 == "-v"):
    %args::remove index 1
    %verbose = (yes)

%to_run = [..]
    :
        for %path in (command line args):
            for %filename in (files for %path):
                if (%filename == "-"): %filename = "stdin"
                if ((%filename::matches "%.nom$") or (%filename == "stdin")):
                    add %filename
# Make sure all the files get run
for %filename in %to_run: use %filename
%tests = {: for %s = %t in (tests): add (=lua "Source:from_string(\%s)") = %t}
for %filename in %to_run:
    %file_tests = []
    for %src = %test in %tests:
        if (%src.filename == %filename):
            %file_tests::add {test: %test, source: %src}
    
    unless (%file_tests is empty):
        sort %file_tests by % -> %.source
        lua> "io.write('[ .. ] ', \%filename); io.flush()"
        if %verbose: say ""
        for % in %file_tests:
            if %verbose:
                say "    \(yellow (%.test::with "\n" -> "\n    "))"
            run %.test
        
        if %verbose:
            say (green "PASS")
        ..else:
            say "\r[\(green "PASS")"
