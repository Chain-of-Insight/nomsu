#!/usr/bin/env nomsu -V6.14
#
    Tool to run all tests in a file (i.e. the code block inside a call to 'test $'). Usage:
    nomsu tools/test.nom file1 file2 directory1 ...
    
use "lib/os"
use "lib/consolecolor"

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

for $filename in $(COMMAND LINE ARGS).extras:
    $(test environment) = (new environment)
    $(test environment), use $filename
    $file = (read file $filename)
    $version =
        $file, matching ("
            #![^
            ]* nomsu %-V[ ]*([^
            ]*)
        ")
    $file_tests = []
    for $src = $test in $(test environment).TESTS:
        if $version:
            $test = ("
                #!/usr/bin/env nomsu -V\$version
                \$test
            ")
        $file_tests, add {.test = $test, .source = $src}
    
    unless ($file_tests is empty):
        sort $file_tests by $ -> $.source
        lua> "io.write('[ .. ] ', \$filename); io.flush()"
        
        if (command line args).v: say ""
        
        for $ in $file_tests:
            if (command line args).v:
                say "    \(yellow ($.test, with "\n" -> "\n    "))"
            $(test environment), run $.test
        
        if (command line args).v:
            say (green "PASS")
        ..else:
            say "\r[\(green "PASS")"
