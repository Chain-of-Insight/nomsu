#!/usr/bin/env nomsu -V2.5.4.3
use "lib/os.nom"
use "lib/consolecolor.nom"

%args = (command line args)
if (%args.1 == "-v"):
    remove index 1 from %args
    %verbose = (yes)

# Make sure all the files get run
for %path in (command line args):
    for file %filename in %path:
        if (%filename matches "%.nom$"): use %filename

%tests = ((=lua "Source:from_string(\%s)") = %t for %s = %t in (tests))
for %path in (command line args):
    for file %filename in %path:
        unless (%filename matches "%.nom$"): do next %filename
        %file_tests = []
        for %src = %test in %tests:
            if (%src.filename == %filename):
                add {test:%test, source:%src} to %file_tests
        
        unless (%file_tests is empty):
            sort %file_tests by % -> %.source
            lua> "io.write('[ .. ] ', \%filename); io.flush()"
            if %verbose: say ""
            for % in %file_tests:
                if %verbose:
                    say "    \(yellow (%.test with "\n" replaced by "\n    "))"
                run %.test
            
            if %verbose: say (green "PASS")
            ..else:
                say "\r[\(green "PASS")"
