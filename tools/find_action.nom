#!/usr/bin/env nomsu -V2.5.4.3
use "core"
use "lib/os.nom"
use "lib/consolecolor.nom"

%stub = (command line args).1
say "Looking for stub: \%stub (from \(command line args))"
%files = ((command line args).% for % in 2 to (|| (command line args) ||))
for %path in %files:
    for file %filename in %path:
        unless (%filename matches "%.nom$") (do next %filename)
        %file = (read file %filename)
        %tree = (parse %file from %filename)
        for %t in recursive %tree:
            if (%t is "Action" syntax tree) (..)
                if (%t.stub is %stub):
                    %line_num = (line number of %t.source.start in %file)
                    say (blue "\%filename:\%line_num:")
                    say (yellow (source lines of %t))
            
            if (%t is syntax tree):
                for %sub in %t: recurse %t on %sub
