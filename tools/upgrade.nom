#!/usr/bin/env nomsu -V4.8.10
#
    Tool to automatically update code from old versions of Nomsu. Usage:
    nomsu tools/upgrade.nom [-i] file1 file2 directory1 ...
    If "-i" is the first argument, upgrades will be performed in-place. Otherwise, the
    upgraded code will be printed.

use "compatibility"
use "lib/os.nom"

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

%args = (command line args)
%inplace = (no)
%start_version = (nil)
%version = (Nomsu version)
repeat:
    if %args.1 is:
        "-i":
            %inplace = (yes)
            %args::remove index 1
        
        "-t":
            use "lib/consolecolor.nom"
            %test = (yes)
            %args::remove index 1
        
        "-V":
            %version = %args.2
            %args::remove index 1
            %args::remove index 1
        
        "-S":
            %start_version = %args.2
            %args::remove index 1
            %args::remove index 1
        
        else: stop

for %path in %args:
    for file %filename in %path:
        unless (%filename::matches "%.nom$"): do next %filename
        %file = (read file %filename)
        %code = (%NomsuCode::from (%Source %filename 1 (size of %file)) %file)
        %tree = (%code parsed)
        %uptree = (..)
            %tree upgraded from (%start_version or (%tree.version or (Nomsu version))) to %version
        %text = ((%uptree as nomsu)::text)
        
        when:
            %inplace:
                say "Upgraded \%filename"
                write %text to file %filename
            
            %test:
                if (%uptree == %tree):
                    say (dim "\%filename will not be changed")
                ..else:
                    say (bright "\%filename will be changed")
            
            else: say %text
