#!/usr/bin/env nomsu -V7
###
    This file defines upgrades from Nomsu <7 to 7
    
use "compatibility/compatibility"

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

upgrade action ($tree with $t -> $replacement) to "7" as
    $tree, with ($t -> $replacement)

upgrade action [
    tree $tree with vars $replacements, $tree with vars $replacements
] to "7" as ($tree, with $replacements)

upgrade action ($tree has subtree $match_tree) to "7" as
    $tree, contains $match_tree

upgrade action (for $var in $iterable at $i $body) to "7" as
    for ($i = $var) in $iterable $body

upgrade action (for $k = $v in $iterable $body) to "7" as
    for ($k = $v) in $iterable $body

upgrade action [
    for $var in $start to $stop by $step $body
    for $var in $start to $stop via $step $body
] to "7" as (for $var in ($start to $stop by $step) $body)

upgrade action (for $var in $start to $stop $body) to "7" as
    for $var in ($start to $stop) $body

upgrade $tree to "7" as:
    if ($tree.type == "EscapedNomsu"):
        $t =
            "Action" tree from $tree.source with ("Text" tree with $tree.1.type) "tree" "with"
        for $tok in $tree.1:
            if ($tok is "Text"): $t, add ("Text" tree with $tok)
            ..else: $t, add $tok
        return $t

upgrade action "Nomsu version" to "7" via ->(`$(NOMSU VERSION))
upgrade action [
    "Nomsu syntax version", "Nomsu compiler version", "core version", "lib version"
] to "7" via
    for $:
        at $.source fail ("
            Deprecation error: Actions for accessing specific parts of the version number have been deprecated.
            Hint: Use $(NOMSU VERSION).1, etc. instead.
        ")
