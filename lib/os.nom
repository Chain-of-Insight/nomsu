#!/usr/bin/env nomsu -V2.3.4.3
#
    This file defines some actions that interact with the operating system and filesystem.

use "core"
action [path of Nomsu file %filename]:
    lua> "for i,f in files.walk(\%filename) do return f end"
    barf "Could not find file: \%filename"

action [sh> %cmd]:
    lua> ".."
        local result = io.popen(\%cmd)
        local contents = result:read("*a")
        result:close()
        return contents

action [read file %filename] (=lua "files.read(\%filename)")
compile [for file %f in %path %body] to (..)
    Lua ".."
        for i,\(%f as lua expr) in files.walk(\(%path as lua expr)) do
            \(%body as lua statements)
            \(compile as (===next %f ===))
        end
        \(compile as (===stop %f ===))

compile [%expr for file %f in %path] to (..)
    Lua value ".."
        (function()
            local ret = list{}
            for i,\(%f as lua expr) in files.walk(\(%path as lua expr)) do
                ret[#ret+1] = \(%expr as lua statements)
            end
            return ret
        end)()

action [..]
    write to file %filename %text, to file %filename write %text
    write %text to file %filename
..:
    lua> "local file = io.open(\%filename, 'w')\nfile:write(\%text)\nfile:close()"

action [line number of %pos in %str] (=lua "files.get_line_number(\%str, \%pos)")
action [line %line_num in %str] (=lua "files.get_line(\%str, \%line_num)")